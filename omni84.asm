************************* 
* OMNISTAR - JUNE 1984  *
* (C) COPYRIGHT 1984    *
* BY RICK ADAMS.        *
*************************
TIMER	EQU	180
ULROW	EQU	181
ULCOL	EQU	182
LRROW	EQU	183
LRCOL	EQU	184
CURROW	EQU	185
CURCOL	EQU	186
IRQTIM	EQU	187
WINDOW	EQU	188
NWINS	EQU	189
NODE	EQU	190
X1	EQU	191
Y1	EQU	192
X2	EQU	193
Y2	EQU	194
SCRN	EQU	195
*UNUSED	197-199
BUFPTR	EQU	200
COUNT	EQU	202
VERB	EQU	203
OBJECT	EQU	209
SLIST	EQU	215
 
	ORG	$3400
WINTBL	RMB	9*10
QUETBL	RMB	9*32
BUFFER	RMB	40
	RMB	1
ULIST	RMB	10
SLISTS	RMB	20*11
	
	ORG	$3C00
START	ORCC	#$50
	CLRB
	TFR	B,DP
	JSR	$A9A2
	JSR	$A976
	LDA	#$F8
	STA 	$FF22
	STA 	$FFC5
	STA 	$FFC3
	STA 	$FFC0
	LBSR	BLDMAP
	CLR 	ULIST-1
	LDA	#20
	STA 	NODE
	LBSR	ININIT
	LBSR	PCLS
	LBSR	WINIT
	LDX	SLIST
	LDA	#7
	LBSR	LIST
	LDA	#1
	ORA 	$FF03
	STA 	$FF03
*SET UP TEXT WINDOW
	LEAX	BUFFER,PCR
	STX	BUFPTR
	CLR	COUNT
	LDA	#5
	STA	WINDOW
	LDB	#10
	MUL
	LEAU	WINTBL,PCR
	LEAU	D,U
	LDD	,U
	STD	ULROW
	LDD	2,U
	STD	LRROW
	LDD	4,U
	STD	CURROW
AGAIN	LBSR	KEYIN
	CMPA	#$03
	BEQ	QUIT
	CMPA	#$0D
	BNE	AGAIN
	LBSR	PARSE
	LBSR	DISP
	BRA	AGAIN
QUIT	ORCC	#$50
	SWI

*CLEAR ENTIRE SCREEN
*
PCLS	LDU	SCRN
	PSHS	Y
	LEAY	6144,U
	PSHS	Y
	LDD	#-1
PCLS1	STD	,U++
	STD	,U++
	STD	,U++
	STD	,U++
	CMPU	,S
	BNE	PCLS1
	PULS	Y
	PULS	Y
	RTS

*OUTPUT TO WINDOW
*       A=CHARACTER
*
OUT	PSHS	A
	LDA	CURCOL
	CMPA	LRCOL
	BLO	OUTX
	BSR	CR
OUTX	PULS	A
	CMPA	#$0D
	BNE	OUT0
*CARRIAGE RETURN
CR	LDA	ULCOL
	STA	CURCOL
	LDA	CURROW
	ADDA	#8
	STA	CURROW
	CMPA	LRROW
	BLS	NOSCRL
	LBSR	WSCROL
NOSCRL	RTS
OUT0	CMPA	#$09
	BNE	OUT1
*TAB
TAB	LDA	CURCOL
	ADDA	#$40
	ANDA	#$C0
	STA	CURCOL
	CMPA	LRCOL
	BHI	CR
	CMPA	ULCOL
	BLO	CR
	RTS
OUT1	CMPA	#$08
	BNE	OUT2
*BACKSPACE
	LDA	CURCOL
	CMPA	ULCOL
	BLS	NOBS
	SUBA	#7
	STA	CURCOL
NOBS	RTS
OUT2	CMPA	#$0C
	BNE	OUT3
*CLEAR
	LBSR	WCLEAR
	RTS
OUT3	CMPA	#$FF
	BNE	OUT4
*CENTER STRING
	LBSR	CENTER
	RTS
OUT4	LEAU	ALPHA,PCR
	SUBA	#$20
	LDB	#5
	PSHS	B
	MUL
	LEAU	D,U
LOOP0	LDX	CURROW
	DEC	,S
	BLT	DONE
	TFR	X,D
	PSHS	D
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	SUBD	#32
	TFR	D,X
	LEAY	BITBL,PCR
	LDD	,S
	ANDB	#7
	LDB	B,Y
	COMB
	PSHS	B
	LDA	,U+
	LSRA
LOOP	LSLA
	LEAX	32,X
	TSTA
	BGT	LOOP
	BEQ	DONE0
	LDB	,X
	ANDB	,S
	STB	,X
	BRA	LOOP
DONE0	PULS	B
	PULS	X
	INC	CURCOL
	BRA	LOOP0
DONE	PULS	A
	LDA	CURCOL
	ADDA	#2
	STA	CURCOL
XOUT	RTS

BITBL	FDB	$8040
	FDB	$2010
	FDB	$0804
	FDB	$0201

*ERASE ONE CHARACTER
*
ERASE	LDX	CURROW
	PSHS	X
	LDB	#5
	PSHS	B
	LEAY	BITBL,PCR
ERAS0	DEC	,S
	BLT	XERAS
	TFR	X,D
	ANDB	#7
	LDB	B,Y
	PSHS	B
	TFR	X,D
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	TFR	D,U
	PULS	B
	LBSR	ERAS1
	LBSR	ERAS1
	LBSR	ERAS1
	LBSR	ERAS2
	LEAX	1,X
	BRA	ERAS0
XERAS	PULS	A
	PULS	X
	RTS
ERAS1	BSR	ERAS2
ERAS2	TFR	B,A
	ORA	,U
	STA	,U
	LEAU	32,U
	RTS

*DISPLAY CURSOR
*
CURSOR	LEAU	CURS,PCR
	LDB	#5
	PSHS	B
	LBRA	LOOP0

CURS	FDB	$007C
	FDB	$7C7C
	FDB	$00

*SCROLL ENTIRE SCREEN
*
PSCROL	TST	$FF02
W0	TST	$FF03
	BGE	W0
	LDU	SCRN
	LEAY	256,U
SCROL0	LDD	,Y++
	STD	,U++
	LDD	,Y++
	STD	,U++
	LDD	,Y++
	STD	,U++
	LDD	,Y++
	STD	,U++
	CMPY	#$1C00
	BLT	SCROL0
	LDD	#-1
SCROL1	STD	,--Y
	CMPY	#$1B00
	BGT	SCROL1
	LDA	CURROW
	SUBA	#8
	STA	CURROW
	LDA	ULCOL
	STA	CURCOL
	RTS

ALPHA
	FDB	$0000
	FDB	$0000
	FCB	$00
	FDB	$00FA !
	FDB	$FA00
	FCB	$00
	FDB	$00C0 "
	FDB	$00C0
	FCB	$00
	FDB	$28FE #
	FDB	$28FE
	FCB	$28
	FDB	$2454 $
	FDB	$FE54
	FCB	$48
	FDB	$4608 %
	FDB	$1020
	FCB	$C4
	FDB	$046A &
	FDB	$926C
	FCB	$0A
	FDB	$0000 '
	FDB	$20C0
	FCB	$00
	FDB	$0038 (
	FDB	$4482
	FCB	$00
	FDB	$0082 )
	FDB	$4438
	FCB	$00
	FDB	$5438 *
	FDB	$FE38
	FCB	$54
	FDB	$1010 +
	FDB	$7C10
	FCB	$10
	FDB	$0000 ,
	FDB	$020C
	FCB	$00
	FDB	$1010 -
	FDB	$1010
	FCB	$10
	FDB	$0000 .
	FDB	$0200
	FCB	$00
	FDB	$0608 /
	FDB	$1020
	FCB	$C0
	FDB	$6C82 0
	FDB	$8282
	FCB	$6C
	FDB	$0000 1
	FDB	$0000
	FCB	$EE
	FDB	$0C92 2
	FDB	$9292
	FCB	$60
	FDB	$0092 3
	FDB	$9292
	FCB	$6C
	FDB	$E010 4
	FDB	$1010
	FCB	$EE
	FDB	$6092 5
	FDB	$9292
	FCB	$0C
	FDB	$6C92 6
	FDB	$9292
	FCB	$0C
	FDB	$0080 7
	FDB	$8080
	FCB	$6C
	FDB	$6C92 8
	FDB	$9292
	FCB	$6C
	FDB	$6092 9
	FDB	$9292
	FCB	$6C
	FDB	$0000 :
	FDB	$2400
	FCB	$00
	FDB	$0002 ;
	FDB	$2400
	FCB	$00
	FDB	$0010 <
	FDB	$2844
	FCB	$00
	FDB	$2828 =
	FDB	$2828
	FCB	$28
	FDB	$0044 >
	FDB	$2810
	FCB	$00
	FDB	$4080 ?
	FDB	$8A90
	FCB	$60
	FDB	$7C82 @
	FDB	$BAAA
	FCB	$78
	FDB	$0EFE A
	FDB	$9090
	FCB	$FE
	FDB	$FE92 B
	FDB	$92FE
	FCB	$0E
	FDB	$FE82 C
	FDB	$82E2
	FCB	$E6
	FDB	$FEFE D
	FDB	$8282
	FCB	$7C
	FDB	$FE9E E
	FDB	$9292
	FCB	$92
	FDB	$FEF0 F
	FDB	$9090
	FCB	$90
	FDB	$FE82 G
	FDB	$82D2
	FCB	$DE
	FDB	$FE10 H
	FDB	$10FE
	FCB	$0E
	FDB	$000E I
	FDB	$FE00
	FCB	$00
	FDB	$0C0E J
	FDB	$0202
	FCB	$FC
	FDB	$FEFE K
	FDB	$1028
	FCB	$C6
	FDB	$0EFE L
	FDB	$0202
	FCB	$02
	FDB	$FE40 M
	FDB	$2040
	FCB	$FE
	FDB	$FE40 N
	FDB	$2010
	FCB	$FE
	FDB	$FE82 O
	FDB	$828E
	FCB	$FE
	FDB	$FE9E P
	FDB	$9090
	FCB	$60
	FDB	$7C82 Q
	FDB	$8A84
	FCB	$7A
	FDB	$FE90 R
	FDB	$90FE
	FCB	$0E
	FDB	$F696 S
	FDB	$9292
	FCB	$DE
	FDB	$8080 T
	FDB	$FEFE
	FCB	$80
	FDB	$FC02 U
	FDB	$02FE
	FCB	$FC
	FDB	$F804 V
	FDB	$02FC
	FCB	$F8
	FDB	$FE04 W
	FDB	$0804
	FCB	$FE
	FDB	$C628 X
	FDB	$1028
	FCB	$C6
	FDB	$E010 Y
	FDB	$0E10
	FCB	$E0
	FDB	$868A Z
	FDB	$92A2
	FCB	$C2
	FDB	$00FE [
	FDB	$8282
	FCB	$00
	FDB	$C020 \
	FDB	$1008
	FCB	$06
	FDB	$0082 ]
	FDB	$82FE
	FCB	$00
	FDB	$2040 ^
	FDB	$FE40
	FCB	$20
	FDB	$0202
	FDB	$0202
	FCB	$02

*SCROLL WINDOW
*
WSCROL	LDD	ULROW
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	TFR	D,X
	LEAX	256,X
	LDA	ULROW
	LDB	LRCOL
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	TFR	D,Y
	LEAY	256,Y
*FUDGE
	LEAY	1,Y
	LDA	LRROW
	LDB	ULCOL
	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	TFR	D,U
	LEAU	256,U
	PSHS	U
S1	PSHS	Y
	TFR	X,U
S2	LDD	,U++
	STD	-258,U
	CMPU	,S
	BLO	S2
	PULS	Y
	LEAX	32,X
	LEAY	32,Y
	CMPX	,S
	BLO	S1
	LEAX	-256,X
	LEAY	-256,Y
	LDA	#-1
S3	PSHS	Y
	TFR	X,U
S4	STA	,U+
	CMPU	,S
	BLO	S4
	PULS	Y
	LEAX	32,X
	LEAY	32,Y
	CMPX	,S
	BLO	S3
	PULS	U
	LDA	ULCOL
	STA	CURCOL
	LDA	CURROW
	SUBA	#8
	STA	CURROW
	RTS

*PRINT MESSAGE
*	U=MESSAGE
*
MSG	PSHS	U
MSG0	LDU	,S
	LDA	,U+
	BEQ	XMSG
	STU	,S
	LDX	#$2000
DELAY	LEAX	-1,X
*	BNE	DELAY
	LBSR	OUT
	BRA	MSG0
XMSG	PULS	U
	RTS

*INTERRUPT SERVICER
*
IRQ	LDA	$FF03
	BPL	XIRQ
	LDA	$FF02
	DEC	IRQTIM
	BNE	XIRQ
	LDD	ULROW
	PSHS	D
	LDD	LRROW
	PSHS	D
	LDD	CURROW
	PSHS	D
	LDA	#4
	STA	IRQTIM
	LDA	NWINS
	PSHS	A
	LDU	#WINTBL-10
IRQ0	DEC	,S
	BLT	XIRQ0
	LEAU	10,U
	LDX	[6,U]
	BEQ	IRQ0
*ESTABLISH CURRENT WINDOW
	LDD	,U
	STD	ULROW
	LDD	2,U
	STD	LRROW
	LDD	4,U
	STD	CURROW
	LDA	,X+
	BEQ	IRQ1
*OUTPUT CHARACTER
	STX	[6,U]
	PSHS	U
	LBSR	BEEP
	LBSR	OUT
	PULS	U
*UPDATE CURRENT WINDOW
	LDD	CURROW
	STD	4,U
	BRA	IRQ0
*END OF MESSAGE
IRQ1	LDD	8,U
	SUBD	#2
	STD	8,U
	LDY	#8
	LDX	6,U
IRQ2	LEAY	-1,Y
	BEQ	XIRQ2
	LDD	2,X
	STD	,X++
	BRA	IRQ2
XIRQ2	LDD	#0
	STD	,X
	BRA	IRQ0
XIRQ0	PULS	A
	PULS	D
	STD	CURROW
	PULS	D
	STD	LRROW
	PULS	D
	STD	ULROW
XIRQ	RTI

*PRINT INTO WINDOW
*	A=WINDOW (0-7)
*	X=MESSAGE
PRINT	LDU	#WINTBL
	LDB	#10
	MUL
	LEAU	D,U
	ORCC	#$10
	STX	[8,U]
	LDD	8,U
	ADDD	#2
	STD	8,U
	ANDCC	#$EF
	RTS

*INITIALIZE WINDOWS
*
WINIT	ORCC	#$10
	CLR	NWINS
	LDA	#4
	STA	IRQTIM
	LDD	HOOK
	STD	$10C
	LDA	HOOK+2
	STA	HOOK+2
	STA	$10E
	LEAX	WDATA,PCR
	LDY	#QUETBL
	LDU	#WINTBL
WINIT0	LDA	,X+
	BLT	XWINIT
	INC	NWINS
	LDB	#8
	MUL
	STB	,U
	STB	4,U
	STB	ULROW
	LDA	,X+
	LDB	#7
	MUL
	STB	1,U
	STB	5,U
	STB	ULCOL
	LDA	,X+
	LDB	#8
	MUL
	STB	2,U
	STB	LRROW
	LDA	,X+
	LDB	#7
	MUL
	STB	3,U
	STB	LRCOL
	STY	6,U
	STY	8,U
	LEAY	16,Y
	LEAU	10,U
	LBSR	WBORDR
	LBSR	WCENTR
	BRA	WINIT0
XWINIT	LDU	#QUETBL
	LDA	#32
	PSHS	A
	CLRA
	CLRB
WINIT2	DEC	,S
	BLT	XXWINI
	STD	,U++
	STD	,U++
	BRA	WINIT2
XXWINI	PULS	A
	RTS

HOOK	JMP	IRQ

WDATA
*WINDOW #0
	FCB	1
	FCB	2
	FCB	5
	FCB	8

*WINDOW #1
	FCB	1
	FCB	11
	FCB	5
	FCB	18

*WINDOW #2
	FCB	1
	FCB	20
	FCB	5
	FCB	27

*WINDOW #3
	FCB	1
	FCB	29
	FCB	5
	FCB	35

*WINDOW #4
	FCB	7
	FCB	2
	FCB	13
	FCB	35

*WINDOW #5
	FCB	15
	FCB	2
	FCB	16
	FCB	35

*WINDOW #6
	FCB	18
	FCB	2
	FCB	22
	FCB	18

*WINDOW #7
	FCB	18
	FCB	20
	FCB	22
	FCB	35

	FCB	-1

*COMPUTE WINDOW CENTER
*
WCENTR	PSHS	X,Y
	LDD	ULROW
	PSHS	D
	LDD	LRROW
	SUBA	,S
	SUBB	1,S
	LSRA
	LSRB
	ADDA	,S+
	ADDB	,S+
	ADDA	#3
*CENTER X,Y ARE IN B,A
	PULS	X,Y
	RTS

*DRAW WINDOW BORDER
*
WBORDR	PSHS	X,Y
	LDD	ULROW
	LBSR	GENPTR
	TFR	D,X
	LEAX	-1,X
	LEAX	-32*3,X
	LDA	ULROW
	LDB	LRCOL
	LBSR	GENPTR
	TFR	D,Y
	LEAY	1,Y
	LEAY	-32*3,Y
	LDD	#$F87F
	ANDA	,X
	STA	,X
	ANDA	,Y
	STB	,Y
	PSHS	X,Y
	LDA	LRROW
	LDB	ULCOL
	LBSR	GENPTR
	TFR	D,X
	LEAX	-1,X
	LEAX	32*6,X
	LEAX	32*3,X
	LDD	LRROW
	LBSR	GENPTR
	TFR	D,Y
	LEAY	1,Y
	LEAY	32*6,Y
	LEAY	32*3,Y
	LDD	#$F81F
	ANDA	,X
	STA	,X
	ANDB	,Y
	STB	,Y
	STB	32,Y
	PSHS	X,Y
	LBSR	VLINE
	LDX	,S
	LDY	2,S
	LBSR	HLINE
	LDX	4,S
	LDY	6,S
	LBSR	HLINE
	LDX	,S
	LDY	2,S
	LEAX	32,X
	LEAY	32,Y
	LBSR	HLINE
	LEAS	8,S
	PULS	X,Y
	RTS

*GENERATE BYTE POINTER
*
GENPTR	LSRA
	RORB
	LSRA
	RORB
	LSRA
	RORB
	ADDD	SCRN
	RTS

HLINE	LEAX	1,X
HLINE0	CLR	,X+
	PSHS	Y
	CMPX	,S++
	BNE	HLINE0
	RTS

VLINE	LDX	6,S
	LDY	8,S
	LEAX	32,X
	LEAY	32,Y
VLINE0	LDA	,X
	ANDA	#$FB
	STA	,X
	LDB	,Y
	ANDB	#$1F
	STB	,Y
	LEAX	32,X
	LEAY	32,Y
	CMPX	2,S
	BLS	VLINE0
	RTS

*DRAW ONE PIXEL
*
PSET	PSHS	B
	LBSR	GENPTR
	TFR	D,X
	LEAY	BITBL,PCR
	PULS	B
	ANDB	#7
	LDA	B,Y
	COMA
	ANDA	,X
	STA	,X
	RTS

*MAKE KEYBEEP SOUND
*
BEEP	PSHS	D,X,Y,U
	LEAS	-10,S
	TFR	A,B
	SUBB	#'0
	CLRA
	LEAU	TONES,PCR
	ASLB
	LEAU	D,U
	PSHS	U
	CLRB
	JSR	$A9A2
	JSR	$A976
	PULS	U
	CLRA
	CLRB
	STD	,S
	STD	2,S
MULUP1	LDA	#2
	STA	8,S
	LEAY	NOTES,PCR
	LDA	,U+
	LDD	A,Y
	STD	4,S
	LDA	,U+
	LDD	A,Y
	STD	6,S
MULUP2	LDY	#40
MULUP3	LDX	#0
	LDB	,S
	BPL	MU1
	COMB
MU1	ABX
	LDB	2,S
	BPL	MU2
	COMB
MU2	ABX
	TFR	X,D
*	LSRA
*	RORB
	STB	$FF20
	LDD	,S
	ADDD	4,S
	STD	,S
	LDD	2,S
	ADDD	6,S
	STD	2,S
	LEAY	,-Y
	BNE	MULUP3
	DEC	8,S
	BNE	MULUP2
MUDUN	LEAS	10,S
	PULS	D,X,Y,U
	RTS

NOTES	FDB 0
	FDB $26F
	FDB $294
	FDB $2EB
	FDB $2E4
	FDB $310
	FDB $33F
	FDB $371
	FDB $3A5
	FDB $3DC
	FDB $417
	FDB $456
	FDB $498
	FDB $4DD
	FDB $528
	FDB $576
	FDB $5C9
	FDB $621
	FDB $67F
	FDB $6E2
	FDB $74A
	FDB $7B9
	FDB $82F
	FDB $8AC
	FDB $930
	FDB $9BB
	FDB $A50
	FDB $AED
	FDB $B93
	FDB $C43
	FDB $CFE
	FDB $DC4
	FDB $E95
	FDB $F73
	FDB $105F
	FDB $1158
	FDB $1260
	FDB $1377
	FDB $14A0
	FDB $15DA
	FDB $1726
	FDB $1887
	FDB $19FC
	FDB $1B88
	FDB $1D2B
	FDB $1EE7
	FDB $20BE
	FDB $22B0
	FDB $24C0


TONES	FDB $3A32
	FDB $3C32
	FDB $4032
	FDB $3A34
	FDB $3C34
	FDB $4034
	FDB $3A36
	FDB $3C36
	FDB $4036

*CLEAR CURRENT WINDOW
*
WCLEAR	LDA	ULROW
	LDB	LRCOL
	LBSR	GENPTR
	SUBD	#32
	PSHS	D
	LDD	ULROW
	LBSR	GENPTR
	SUBD	#32
	PSHS	D
	LDD	LRROW
	LBSR	GENPTR
	ADDD	#256
	TFR	D,U
WCLR1	LDX	,S
	LEAX	32,X
	LDY	2,S
	LEAY	32,Y
	PSHS	U
	CMPY	,S++
	BHI	WCLR3
	STX	,S
	STY	2,S
	LDA	#-1
WCLR2	STA	,X+
	PSHS	Y
	CMPX	,S++
	BLS	WCLR2
	BRA	WCLR1
WCLR3	LEAS	4,S
	LDD	ULROW
	STD	CURROW
	RTS

*CENTER LINE OF TEXT
*
CENTER	PSHS	D,X,Y
	LDY	#0
	CLRB
CENTR1	LDA	,X+
	BEQ	CENTR2
	CMPA	#$0D
	BEQ	CENTR2
	LEAY	-7,Y
	BRA	CENTR1
CENTR2	CLRA
	LDB	ULCOL
	PSHS	D
	LDB	LRCOL
	SUBD	,S++
	ASRA
	RORB
	ADDB	ULCOL
	CLRA
	EXG	D,Y
	ASRA
	RORB
	PSHS	Y
	ADDD	,S++
	CMPD	ULCOL
	BHS	XCENTR
	LDB	ULCOL
XCENTR	STB	CURCOL
	PULS	D,X,Y
	RTS

*GET KEYBOARD INPUT
*
KEYIN	INC	TIMER
	BLT	KEYIN1
	LBSR	ERASE
	BRA	KEYIN2
KEYIN1	LBSR	CURSOR
	LDA	CURCOL
	SUBA	#7
	STA	CURCOL
KEYIN2	JSR	[$A000]
	PSHS	A
	BEQ	XKEYIN
	STA	$FFCC
	STA	$FFCA
	LBSR	BEEP
	LDA	,S
	CMPA	#$0C
	BNE	KEYIN3
	LDB	COUNT
	NEGB
	BRA	KEYIN6
KEYIN3	CMPA	#$0D
	BNE	KEYIN4
	LDB	COUNT
	NEGB
	BRA	KEYIN6
KEYIN4	CMPA	#$08
	BNE	KEYIN5
	LDB	#$FF
	BRA	KEYIN6
KEYIN5	LDB	#1
KEYIN6	ADDB	COUNT
	CMPB	#32
	BHI	XKEYIN
	STB	COUNT
	LDA	,S
	STA	[BUFPTR]
	LEAX	BUFFER,PCR
	LEAX	B,X
	STX	BUFPTR
	LBSR	ERASE
	LDA	,S
	LBSR	OUT
XKEYIN	PULS	A
	TSTA
	RTS

*COMMAND PARSER
*
PARSE	LDD	#$2020
	STD	VERB
	STD	VERB+2
	STD	VERB+4
	STD	OBJECT
	STD	OBJECT+2
	STD	OBJECT+4
	LDX	#BUFFER
	LDA	#5
	LDU	#VERB
PARSE0	LDB	,X+
	CMPB	#$0D
	BEQ	XPARSE
	CMPB	#$20
	BEQ	PARSE1
	DECA
	BLT	PARSE1
	STB	,U+
	BRA	PARSE0
PARSE1	LDU	#OBJECT
	LDA	#5
PARSE2	LDB	,X+
	CMPB	#$0D
	BEQ	XPARSE
	CMPB	#$20
	BEQ	XPARSE
	DECA
	BLT	XPARSE
	STB	,U+
	BRA	PARSE2
XPARSE	RTS

*COMMAND DISPATCHER
*
DISP	LDA	BUFFER
	CMPA	#$0D
	BEQ	XDISP
	LBSR	GENKEY
	LEAU	TABLE,PCR
	LEAU	-4,U
DISP0	LEAU	4,U
	TST	,U
	BLT	ERR
	CMPD	,U
	BNE	DISP0
	LEAU	2,U
	LDD	,U
	LEAU	D,U
	PSHS	U
	LBSR	LOOKUP
	PULS	U
	JSR	,U
	RTS
ERR	LEAU	ERROR,PCR
	LBSR	MSG
XDISP	RTS

TABLE	FDB	$11B3
	FDB	ENT-.
	FDB	$468D
	FDB	RUN-.
	FDB	$2DC0
	FDB	LOA-.
	FDB	$4815
	FDB	SAV-.
	FDB	$300F
	FDB	MAP-.
	FDB	$FFFF

ERROR
 FCC /SYNTAX ERROR/
	FDB	$0D00

GENKEY	CLR	,-S
	CLR	,-S
	LDA	VERB
	SUBA	#'A
	LSLA
	LSLA
	CLRB
	STD	,S
	LDB	VERB+1
	SUBB	#'A
	LDA	#32
	MUL
	ORA	,S
	ORB	1,S
	STD	,S
	CLRA
	LDB	VERB+2
	SUBB	#'A
	ORA	,S+
	ORB	,S+
	RTS

RUN
	LDA	#4
	LEAX	M1,PCR
	LBSR	PRINT
	RTS

M1 FCC /COMMAND OKAY/
	FDB	$0D00

*LIST INVENTORY CONTENTS
*	A=WINDOW (0-7)
*	X=DATA LIST
*
LIST	ORCC	#$10
	LDU	#WINTBL
	LDB	#10
	MUL
	LEAU	D,U
	LDD	ULROW
	PSHS	D
	LDD	LRROW
	PSHS	D
	LDD	CURROW
	PSHS	D
	LDD	,U
	STD	ULROW
	LDD	2,U
	STD	LRROW
	LDD	4,U
	STD	CURROW
	LDA	#$0C
	PSHS	X
	LBSR	OUT
	PULS	X
	LDA	-1,X
	PSHS	A
LIST0	DEC	,S
	BLT	XLIST
	LDB	,X+
	LEAU	WORDS,PCR
	LDA	#5
	MUL
	LEAU	D,U
	LDB	#5
LIST1	DECB
	BLT	XLIST1
	LDA	,U+
	PSHS	B,X,U
	LBSR	OUT
	PULS	B,X,U
	BRA	LIST1
XLIST1	TST	,S
	BEQ	LIST0
	LDA	#$09
	PSHS	X
	LBSR	OUT
	PULS	X
	BRA	LIST0
XLIST	PULS	A
	PULS	D
	STD	CURROW
	PULS	D
	STD	LRROW
	PULS	D
	STD	ULROW
	ANDCC	#$EF
	RTS

WORDS
	FCC /PORT1/
	FCC /PORT2/
	FCC /PORT3/
	FCC /SVZYD/
	FCC /NQSLW/
	FCC /MZLCM/
	FCC /CLLTF/
	FCC /BLNFJ/
	FCC /TTSLX/
	FCC /LNDVN/
	FCC /KCYRK/
	FCC /DXDJC/
	FCC /LYMGC/
	FDB $FFFF

*LOOK UP OBJECT
*	A=OBJECT CODE
*
LOOKUP	LEAU	WORDS,PCR
	CLR	,-S
LOOKU0	TFR	U,Y
	LDX	#OBJECT
	LDA	#5
	PSHS	A
LOOKU1	DEC	,S
	BLT	LOOKU2
	LDA	,X+
	CMPA	,Y+
	BEQ	LOOKU1
	PULS	A
	LEAU	5,U
	INC	,S
	LDD	,U
	CMPD	#$FFFF
	BNE	LOOKU0
	LDA	#$FF
	STA	,S
	BRA	LOOKU3
LOOKU2	PULS	A
LOOKU3	PULS	A
	RTS

*SEARCH LIST
*	X=LIST
*	A=ITEM
*
SEARCH	LDB	-1,X
SEARC0	DECB
	BLT	SEARC1
	CMPA	,X+
	BNE	SEARC0
SEARC1	RTS

*LOAD PROGRAM FROM SYSTEM
*	A=PROGRAM CODE
*
LOA	LDB	ULIST-1
	CMPB	#10
	BLT	LOA0
	LEAU	FULL,PCR
	LBSR	MSG
	RTS
LOA0	LDX	#ULIST
	LBSR	SEARCH
	BLT	LOA1
	LEAU	LOADD,PCR
	LBSR	MSG
	RTS
LOA1	LDX	SLIST
	LBSR	SEARCH
	BGE	LOA2
	LEAU	NOPRG,PCR
	LBSR	MSG
	RTS
LOA2	LDB	ULIST-1
	LDX	#ULIST
	LEAX	B,X
	STA	,X
	INC	ULIST-1
	PSHS	A
	LDA	#6
	LDX	#ULIST
	LBSR	LIST
	LDA	,S+
	LDX	SLIST
	LBSR	DELL
	LDX	SLIST
	LDA	#7
	LBSR	LIST
	LDA	#4
	LEAX	LOAOK,PCR
	LBSR	PRINT
	RTS

LOAOK
 FCC /PROGRAM LOADED /
 FCC /FROM SYSTEM/
 FDB $0D00

FULL
 FCC /USER PROGRAM AREA /
 FCC /FULL/
 FDB $0D00
LOADD
 FCC /PROGRAM ALREADY /
 FCC /LOADED/
 FDB $0D00
NOPRG
 FCC /PROGRAM NOT IN /
 FCC /SYSTEM/
 FDB $0D00

*DELETE ITEM FROM LIST
*	X=LIST
*	A=ITEM
*
DELL	TFR	X,Y
	LDB	-1,X
DELL0	DECB
	BLT XDELL
	CMPA	,X+
	BNE	DELL0
DELL1	LDA	,X
	STA	-1,X
	LEAX	1,X
	DECB
	BGT	DELL1
	DEC	-1,Y
XDELL	RTS

*SAVE PROGRAM TO SYSTEM
*	A=PROGRAM CODE
*
SAV	LDX	#ULIST
	LBSR	SEARCH
	BGE	SAV0
	LEAU	NOTIN,PCR
	LBSR	MSG
	RTS
SAV0	LDX	SLIST
	LBSR	SEARCH
	BLT	SAV1
	LEAU	INSYS,PCR
	LBSR	MSG
	RTS
SAV1	LDX	SLIST
	LDB	-1,X
	CMPB	#10
	BLT	SAV2
	LEAU	SFULL,PCR
	LBSR	MSG
	RTS
SAV2	INCB
	STB	-1,X
	LEAX	B,X
	STA	-1,X
	LDX	#ULIST
	LBSR	DELL
	LDA	#6
	LDX	#ULIST
	LBSR	LIST
	LDA	#7
	LDX	SLIST
	LBSR	LIST
	LEAX	SAVOK,PCR
	LDA	#4
	LBSR	PRINT
	RTS

SAVOK
 FCC /PROGRAM SAVED /
 FCC /TO SYSTEM/
 FDB $0D00

NOTIN
 FCC /NOT IN USER /
 FCC /PROGRAM AREA/
 FDB $0D00

INSYS
 FCC /PROGRAM ALREADY /
 FCC /IN SYSTEM/
 FDB $0D00

SFULL
 FCC /SYSTEM PROGRAM /
 FCC /AREA FULL/
 FDB $0D00

*ENTER NEW NODE
*	A = PORT (0-2)
*
ENT	CMPA	#2
	BLS	ENT0
	LEAU	BAD,PCR
	LBSR	MSG
	RTS
ENT0	LEAU	TLIST,PCR
	PSHS	A
	LDA	NODE
	DECA
	LDB	#3
	MUL
	LEAU	D,U
	PULS	A
	LDA	A,U
	SUBA	#'A
	INCA
	STA	NODE
	DECA
	LDB	#11
	MUL
	LDX	#SLISTS
	LEAX	D,X
	LEAX	1,X
	STX	SLIST
	LBSR	PRNAME
	LDX	SLIST
	LDA	#7
	LBSR	LIST
	RTS

BAD
 FCC /BAD PORT /
 FCC /DESIGNATION/
 FDB $0D00

*TRANSITION LIST
*
TLIST
 FCC /BEHACJBDLCENADF/
 FCC /EGOFHQAGIHJRBIK/
 FCC /JLSCKMLNTDMOFNP/
 FCC /OQTGPRIQSKRTMPS/

*INVENTORY INITIALIZATION
*
ININIT	LEAX	IDATA,PCR
	LDY	#SLISTS+1
	LDA	#20
	PSHS	A
ININI0	DEC	,S
	BLT	ININI3
	CLR	-1,Y
	STY	SLIST
	CLRB
ININI1	LDA	,X+
	SUBA	#'A
	BLT	ININI2
	STA	B,Y
	INCB
	STB	-1,Y
	BRA	ININI1
ININI2	LEAY	11,Y
	BRA	ININI0
ININI3	PULS	A
	RTS

*INVENTORY DATA
*
IDATA
 FCC /EFGH'M'D'HIJKLM'D'/
 FCC /DEFGHIJ'M'I'H'F'/
 FCC /D'E'F'G'H'/
 FCC /E'F'G'M'DEFGHIJKLM'/

*PRINT SYSTEM NAME
*
PRNAME	LEAX	CLR,PCR
	LDA	#4
	LBSR	PRINT
	LBSR	GENNAME
	LDA	#4
	LBSR	PRINT
	LEAX	CRLF,PCR
	LDA	#4
	LBSR	PRINT
	RTS

CRLF	FDB	$0D00
CLR	FDB	$0C00

*GENERATE SYSTEM NAME
*
GENNAME	LEAX	NAMES,PCR
	LDA	NODE
GENAM0	DECA
	BEQ	GENAM2
GENAM1	LDB	,X+
	BNE	GENAM1
	BRA	GENAM0
GENAM2	RTS

NAMES
 FCC /SECURITY CONTROL/
 FCB 0
 FCC /SATELLITE NAVIGATION/
 FCB 0
 FCC /NETWORK CONTROL/
 FCB 0
 FCC /LOGIC CONTROL/
 FCB 0
 FCC /MAINTENANCE CONTROL/
 FCB 0
 FCC /AUX MAINTENANCE CONTROL/
 FCB 0
 FCC /AUX POWER CONTROL/
 FCB 0
 FCC /AUX SECURITY CONTROL/
 FCB 0
 FCC /AUX SATELLITE COMMUNICATIONS/
 FCB 0
 FCC /AUX SATELLITE NAVIGATION/
 FCB 0
 FCC /AUX ENVIRONMENT CONTROL/
 FCB 0
 FCC /AUX NETWORK CONTROL/
 FCB 0
 FCC /AUX ACCESS COMPUTER/
 FCB 0
 FCC /AUX LOGIC CONTROL/
 FCB 0
 FCC /AUX MEMORY CONTROL/
 FCB 0
 FCC /MEMORY CONTROL/
 FCB 0
 FCC /POWER CONTROL/
 FCB 0
 FCC /SATELLITE COMMUNICATIONS/
 FCB 0
 FCC /ENVIRONMENT CONTROL/
 FCB 0
 FCC /OMNISTAR ACCESS COMPUTER/
 FCB 0

*DISPLAY SYSTEM MAP
*
MAP	ORCC	#$10
	STA	$FFCD
	STA	$FFCB
	ANDCC	#$EF
	RTS

*DRAW LINES
*	Y=LINE DATA
*
DRAW	LDA	,Y
	TSTA
	BNE	DRAW1
	LEAY	1,Y
	LDD	,Y
	TSTA
	BEQ	XDRAW
	BSR	GETXY
	STD	X1
	BRA	DRAW
DRAW1	BSR	GETXY
	STD	X2
	PSHS	Y
	LBSR	LINE
	LBSR	SQUARE
	PULS	Y
	BRA	DRAW
XDRAW	RTS

GETXY	LDD	,Y++
	RTS

LINES
*INNER RING
	FCB 0
	FDB $6E70
	FDB $634F
	FDB $7F3A
	FDB $9C4E
	FDB $9170
	FDB $6E70
*MIDDLE RING
	FCB 0
	FDB $BC6C
	FDB $7F97
	FDB $426B
	FDB $5A23
	FDB $A623
	FDB $BC6C
*OUTER RING
	FCB 0
	FDB $DD76
	FDB $7FB9
	FDB $2276
	FDB $4608
	FDB $B908
	FDB $DD76
*MIDDLE TO OUTER
	FCB 0
	FDB $BC6C
	FDB $DD76
	FCB 0
	FDB $7F97
	FDB $7FB9
	FCB 0
	FDB $426B
	FDB $2276
	FCB 0
	FDB $5A23
	FDB $4608
	FCB 0
	FDB $A623
	FDB $B908
*INNER TO MIDDLE
	FCB 0
	FDB $6E70
	FDB $6181
	FCB 0
	FDB $634F
	FDB $4E47
	FCB 0
	FDB $7F3A
	FDB $7F23
	FCB 0
	FDB $9C4E
	FDB $B148
	FCB 0
	FDB $9170
	FDB $9D82
*MASTER
	FCB 0
	FDB $7F3A
	FDB $7F58

	FDB 0
	FDB 0

*DRAW LINE
*       CO-ORDS IN
*       X1,Y1,X2,Y2
*
LINE    LEAS    -11,S
        CLR     1,S
        CLR     4,S
        LDA     #1
        STA     2,S
        STA     3,S
        CLRA
        LDB     Y1
        STD     7,S
        LDB     Y2
        SUBD    7,S
        STD     7,S
        CLRA
        LDB     X1
        STD     5,S
        LDB     X2
        SUBD    5,S
        STD     5,S
        BGE     L6090
        LDA     #255
        STA     3,S
        CLRA
        CLRB
        SUBD    5,S
        STD     5,S
L6090   LDD     7,S
        BGE     L6120
        CLRA
        CLRB
        SUBD    7,S
        STD     7,S
        LDA     #255
        STA     2,S
L6120   LDD     5,S
        CMPD    7,S
        BGE     L6200
        LDX     5,S
        LDY     7,S
        STX     7,S
        STY     5,S
        LDA     3,S
        STA     1,S
        CLR     3,S
        LDA     2,S
        STA     4,S
        CLR     2,S
L6200   LDD     5,S
        STB     ,S
        LSRA
        RORB
        STD     9,S
LLOOP   LDA     Y1
        LDB     X1
        LBSR    PSET
        DEC     ,S
        BLT     LDONE
        LDD     3,S
        ADDA    X1
        ADDB    Y1
        STA     X1
        STB     Y1
        LDD     9,S
        ADDD    7,S
        STD     9,S
        SUBD    5,S
        BLE     LLOOP
        STD     9,S
        LDD     1,S
        ADDA    X1
        ADDB    Y1
        STA     X1
        STB     Y1
        BRA     LLOOP
LDONE   LEAS    11,S
        RTS

*DRAW NODE ON MAP
*
SQUARE	LDD	X1
	SUBB	#2
	STB	Y1
	ADDA	#2
	STD	X2
	LBSR	LINE
	LDD	X1
	ADDB	#4
	STD	X2
	LBSR	LINE
	LDD	X1
	SUBA	#4
	STD	X2
	LBSR	LINE
	LDD	X1
	SUBB	#4
	STD	X2
	LBSR	LINE
	LDD	X1
	ADDA	#2
	STD	X2
	LBSR	LINE
	INC	Y1
	INC	Y1
	RTS

*BUILD SYSTEM MAP
*
BLDMAP	LDD	#$1C00
	STD 	SCRN
	LBSR	PCLS
	LEAY	LINES,PCR
	LBSR	DRAW
	LDD	#$400
	STD	SCRN
	RTS

ZPROG
	END	START
